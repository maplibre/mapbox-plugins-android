ext {
    mapLibreArtifactGroupId = 'org.maplibre.gl'
    mapLibreArtifactId = project.property('POM_ARTIFACT_ID')
    mapLibreArtifactTitle = project.property('POM_NAME')
    mapLibreArtifactDescription = project.property('POM_DESCRIPTION')
    mapLibreDeveloperName = 'MapLibre'
    mapLibreDeveloperId = 'maplibre'
    mapLibreArtifactUrl = 'https://github.com/maplibre/maplibre-plugins-android'
    mapLibreArtifactVcsUrl = 'git@github.com:maplibre/maplibre-plugins-android.git'
    mapLibreArtifactScmUrl = 'scm:git@github.com:maplibre/maplibre-plugins-android.git'
    mapLibreArtifactLicenseName = 'BSD'
    mapLibreArtifactLicenseUrl = 'https://opensource.org/licenses/BSD-2-Clause'
    versionName = getVersionName()
}

def getVersionName() {
    def cmd = "git describe --tag --abbrev=0"
    def version = cmd.execute().text.trim()
    return (version == null || version.isBlank()) ? "0.0.1" : isSnapshot() ? getSnapshotVersion(version) : getReleaseVersion(version)}

def isSnapshot() {
    return project.hasProperty("snapshot")
            ? project.property("snapshot").toBoolean()
            : false
}

private static def getReleaseVersion(String version) {
    return version.substring(1)
}

private static def getSnapshotVersion(String version) {
    def matcher = version =~ /^[v|V](\d+).(\d+)?.*/
    def major = (matcher[0][1] as Integer)
    def minor = (matcher[0][2] as Integer) + 1
    return "${major}.${minor}.0-SNAPSHOT"
}
